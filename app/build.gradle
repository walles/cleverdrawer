buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath 'io.fabric.tools:gradle:1.28.1'
    }
}

apply plugin: 'com.android.application'

/*
 * MIT License
 *
 * Copyright (c) 2017 Johan Walles <johan.walles@gmail.com>
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 *
 */

// Fabric / Crashlytics must come after the Android plugin
apply plugin: 'io.fabric'
repositories {
    maven { url 'https://maven.fabric.io/public' }
}

// During git hook execution some GIT_ variables are set which make our commands fail unless we
// strip those variables out
def getNoGitEnv = { ->
    def noGitEnv = new HashMap<String, String>(System.getenv())
    def iterator = noGitEnv.entrySet().iterator()
    while (iterator.hasNext()) {
        def entry = iterator.next()
        if (entry.key.startsWith("GIT_")) {
            iterator.remove()
        }
    }

    return noGitEnv
}

// From: http://stackoverflow.com/questions/17097263/automatically-versioning-android-project-from-git-describe-with-android-studio-g
def getVersionCode = { ->
    def output = new ByteArrayOutputStream()
    exec {
        environment = getNoGitEnv()
        standardOutput = output
        commandLine 'git', 'tag', '--list'
    }
    return output.toString().split("\n").size()
}

def getVersionName = { ->
    def output = new ByteArrayOutputStream()
    exec {
        environment = getNoGitEnv()
        standardOutput = output
        commandLine 'git', 'describe', '--tags', '--dirty', '--first-parent'
    }
    return output.toString().trim()
}

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "com.gmail.walles.johan.cleverdrawer"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        // These tests can be fine one day and not fine the next without us changing anything. Not
        // reliable enough, disable.
        //
        // Docs at http://tools.android.com/tips/lint-checks
        disable("GradleDependency")
        disable("NewerVersionAvailable")
        disable("OldTargetApi")

        // Johan doesn't want to generate multiple icon sizes; most phones can resize an icon just
        // fine by itself.
        disable("IconMissingDensityFolder")

        // Johan doesn't care about Kotlin. PRs welcome :)
        disable("UnknownNullness")
        disable("KotlinPropertyAccess")

        // We will never autofill anything here (knock, knock)
        disable("Autofill")

        abortOnError true
        checkAllWarnings true
        warningsAsErrors true

        textReport true
        textOutput "stdout"
        explainIssues false
    }

    // Read signing properties from ~/.gradle/cleverdrawer.properties
    //
    // From: https://www.timroes.de/2013/09/22/handling-signing-configs-with-gradle/
    if (new File("${gradle.gradleUserHomeDir}/cleverdrawer.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("${gradle.gradleUserHomeDir}/cleverdrawer.properties")))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keyAlias']
                keyPassword props['keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'org.jetbrains:annotations:15.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.2'

    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }

    testImplementation 'junit:junit:4.12'

    // For Matchers.empty(), see DatabaseUtilsTest.testScoreLaunchablesPerformance()
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
